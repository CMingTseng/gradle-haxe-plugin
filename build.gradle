group = 'com.prezi.haxe'

if (hasProperty("release")) {
	version = [ "git", "describe", "--match", "[0-9]*", "--dirty"].execute().text.trim()
} else {
	version = [ "git", "describe", "--match", "[0-9]*", "--abbrev=0"].execute().text.trim() + "-SNAPSHOT"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile "org.webjars:requirejs:2.1.8"
	testCompile("org.spockframework:spock-core:0.7-groovy-1.8") {
		exclude group: "org.codehaus.groovy"
	}
}

jar {
	from "LICENSE.txt"
}

task run(type: Exec) {
	dependsOn install
	workingDir "src/test/at"
	commandLine = ["gradle", "clean", "compile", "testJs", "uploadArchives", "--no-daemon", "-Pmunit.kill-browser", "--info", "--stacktrace"]
}

uploadArchives {
	repositories {
		repositories {
			mavenDeployer {
				pom.project {
					url "http://github.com/prezi/gradle-haxe-plugin"
					name project.name
					description project.description

					scm {
						url "scm:git@github.com:prezi/gradle-haxe-plugin.git"
						connection "scm:git@github.com:prezi/gradle-haxe-plugin.git"
						developerConnection "scm:git@github.com:prezi/gradle-haxe-plugin.git"
					}

					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}

					developers {
						developer {
							id "lptr"
							name "Lorant Pinter"
						}
						developer {
							id "Schipy"
							name "Peter Sipos"
						}
						developer {
							id "btorok"
							name "Balazs Torok"
						}
					}
				}

				if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
					def user = project.getProperty("nexusUser")
					def password = project.getProperty("nexusPassword")
					repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
						authentication(userName: user, password: password)
					}
				}
			}
		}
	}
}
